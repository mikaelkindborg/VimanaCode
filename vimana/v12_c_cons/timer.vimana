'lib.vimana' evalfile

/-- Timer global variables --/

() (TimerList) setglobal
0  (TimerCounter) setglobal
1  (TimerRunFlag) setglobal

/-- Timer handling functions --/

(timerCounterIncr)
  (TimerCounter 1+ (TimerCounter) setglobal) def

/-- codelist interval --/
(timerAdd) (
  [ab] timerCounterIncr
  /-- (lastRun interval codelist timerId) --/
  [ millis b a TimerCounter ] TimerList cons
    (TimerList) setglobal) def

(timerCheckAll) 
  (TimerList (timerRun) map) def

(timerStopAll)
  (0 (TimerRunFlag) setglobal) def

(timerClearAll)
  (() (TimerList) setglobal) def

(timerLoop) 
  (TimerRunFlag (
    timerCheckAll 
    10 sleep
    timerLoop) 
  iftrue) def

/-- Timer functions --/

(timerLastRun)    (first) def
(timerInterval)   (rest first) def
(timerCode)       (rest rest first) def
(timerId)         (rest rest rest first) def

(timerLastRunSet) (millis setfirst drop) def

(timerRun)
  ([A] A timerLastRun A timerInterval +
    millis < (
      A timerLastRunSet
      A timerCode call) 
    iftrue) def

/-- Example --/

(sayHi) 1000 timerAdd

[ 0 ] (dup first 1+ setfirst first print) cons
  100 timerAdd

(timerStopAll) 5000 timerAdd

timerLoop

printstack
